+dk hdf5
+if hdf5

! ================================================================================================ !
!  HDF5 Module
! ~~~~~~~~~~~~~
!  Written by:    Veronica Berglyd Olsen, BE-ABP-HSS, 2018
!  Last Modified: 2018-04-13
!
!  Alternative output format.
!  Controlled by the HDF5 input block.
! ================================================================================================ !
module hdf5_output
  
  use hdf5
  use floatPrecision
  use end_sixtrack
  use crcoall
  use strings
  
  implicit none
  
  ! Common Settings
  logical,      public,  save :: h5_isActive, h5_debugOn ! Main module switches
  logical,      private, save :: h5_doTruncate           ! Whether or not to truncate previous file if exists
  type(string), private, save :: h5_fileName             ! The HDF5 output file name
  type(string), private, save :: h5_rootPath             ! The root group where the data for this session is stored
  
  ! Input Block Switches
  logical, public, save :: h5_useForCOLL
  logical, public, save :: h5_useForDUMP
  logical, public, save :: h5_useForSCAT
  
  ! Runtime Variables
  integer,        private, save :: h5_fileError
  logical,        private, save :: h5_fileIsOpen
  integer(HID_T), private, save :: h5_fileID
  integer(HID_T), private, save :: h5_rootID
  
contains

! ================================================================================================ !
!  HDF5 Initialisation
!  V.K. Berglyd Olsen, BE-ABP-HSS
!  Last Modified: 2018-04-16
! ================================================================================================ !
subroutine h5_initHDF5()
  
  use end_sixtrack
  
  implicit none
  
  call h5open_f(h5_fileError)
  if(h5_fileError == -1) then
    write(lout,"(a)") "HDF5> ERROR Failed to initialise Fortran HDF5."
    call prror(-1)
  end if
  
end subroutine h5_initHDF5

! ================================================================================================ !
!  HDF5 File Initialisation
!  V.K. Berglyd Olsen, BE-ABP-HSS
!  Last Modified: 2018-04-16
! ================================================================================================ !
subroutine h5_openFile()
  
  use end_sixtrack
  
  implicit none
  
  integer accessFlag
  
  if(h5_doTruncate) then
    accessFlag = H5F_ACC_TRUNC_F
    if(h5_debugOn) then
      write(lout,"(a)") "HDF5> DEBUG Truncating previous file if it exists."
    end if
  else
    accessFlag = H5F_ACC_EXCL_F
  end if
  
  call h5fcreate_f(h5_fileName%chr, accessFlag, h5_fileID, h5_fileError)
  if(h5_fileError == -1) then
    write(lout,"(3a)") "HDF5> ERROR Failed to open HDF5 file '",h5_fileName%chr,"'."
    call prror(-1)
  end if
  write(lout,"(3a)") "HDF5> Created/opened HDF5 file '",h5_fileName%chr,"'."
  
  ! If a root group was requested, create it and save the rootID
  ! Otherwise, use the fileID as the rootID
  if(h5_rootPath%chr /= "") then
    call h5gcreate_f(h5_fileID, h5_rootPath%chr, h5_rootID, h5_fileError)
    if(h5_fileError == -1) then
      write(lout,"(3a)") "HDF5> ERROR Failed to create root group '",h5_rootPath%chr,"'."
      call prror(-1)
    end if
    write(lout,"(3a)") "HDF5> Created root group '",h5_rootPath%chr,"'."
  else
    h5_rootID = h5_fileID
  end if
  
end subroutine h5_openFile

! ================================================================================================ !
!  HDF5 Finalise
!  V.K. Berglyd Olsen, BE-ABP-HSS
!  Last Modified: 2018-04-16
! ================================================================================================ !
subroutine h5_closeHDF5()
  
  use end_sixtrack
  
  implicit none
  
  call h5fclose_f(h5_fileID, h5_fileError)
  if(h5_fileError == -1) then
    write(lout,"(a)") "HDF5> ERROR Failed to close HDF5 file."
    call prror(-1)
  end if
  
  call h5close_f(h5_fileError)
  if(h5_fileError == -1) then
    write(lout,"(a)") "HDF5> ERROR Failed to close Fortran HDF5."
    call prror(-1)
  end if
  
  write(lout,"(a)") "HDF5> Closed HDF5 file."
    
end subroutine h5_closeHDF5

! ================================================================================================ !
!  HDF5 Input File Parsing
!  V.K. Berglyd Olsen, BE-ABP-HSS
!  Last Modified: 2018-04-13
! ================================================================================================ !
subroutine h5_parseInputLine(inLine)
  
  use string_tools
  use end_sixtrack
  
  implicit none
  
  type(string), intent(in)  :: inLine
  
  type(string), allocatable :: lnSplit(:)
  integer nSplit
  
  integer i
  
  ! Split the input line
  call str_split(inLine,lnSplit,nSplit)
  
  if(nSplit == 0) then
    if(h5_debugOn) then
      write (lout,"(a,i3,a)") "HDF5> DEBUG Input line len=",len(inLine%chr),": '",chr_trim(inLine%chr),"'."
      write (lout,"(a)")      "HDF5> DEBUG  * No fields found."
    end if
    return
  end if
  
  select case(lnSplit(1)%chr)
  
  case("DEBUG")
    h5_debugOn = .true.
    write(lout,"(a)") "HDF5> HDF5 block debugging is ON."
  
  case("FILE")
    if(nSplit < 2 .or. nSplit > 3) then
      write(lout,"(a,i2,a)") "HDF5> ERROR in FILE. Statement takes 1 or 2 input parameters, ",(nSplit-1)," given."
      write(lout,"(a)")      "HDF5> Valid input is FILE filename [truncate]"
      call prror(-1)
    end if
    if(nSplit == 3) then
      read(lnSplit(3)%chr,*) h5_doTruncate
    else
      h5_doTruncate = .false.
    end if
    h5_fileName = str_stripQuotes(lnSplit(2))
    write(lout, "(3a)") "HDF5> Output file name set to: '",h5_fileName%chr,"'."
  
  case("ROOT")
    if(nSplit /= 2) then
      write(lout,"(a,i2,a)") "HDF5> ERROR in ROOT. Statement takes 1 input parameter, ",(nSplit-1)," given."
      call prror(-1)
    end if
    if(str_inStr(lnSplit(2)," ") /= 0) then
      write(lout,"(a)") "HDF5> ERROR in ROOT. Group name cannot contain a space."
      call prror(-1)
    end if
    if(str_inStr(lnSplit(2),"/") /= 0) then
      write(lout,"(a)") "HDF5> ERROR in ROOT. Group name cannot contain a slash."
      call prror(-1)
    end if
    h5_rootPath = str_stripQuotes(lnSplit(2))
    write(lout, "(3a)") "HDF5> Root group set to: '",h5_rootPath%chr,"'."
  
  case("ENABLE")
  
    if(nSplit /= 2) then
      write(lout,"(a,i2,a)") "HDF5> ERROR in ENABLE. Statement takes 1 input parameter, ",(nSplit-1)," given."
      call prror(-1)
    end if
    if(len(lnSplit(2)%chr) < 4) then
      write(lout,"(a,i2,a)") "HDF5> ERROR in ENABLE. Argument must be at least 4 characters."
      call prror(-1)
    end if
    
    select case(lnSplit(2)%chr(1:4))
    case("COLL")
      h5_useForCOLL = .true.
      write(lout,"(3a)") "HDF5> HDF5 is enabled for COLLIMATION."
    case("DUMP")
      h5_useForDUMP = .true.
      write(lout,"(3a)") "HDF5> HDF5 is enabled for DUMP."
    case("SCAT")
      h5_useForSCAT = .true.
      write(lout,"(3a)") "HDF5> HDF5 is enabled for SCATTER."
    case default
      write(lout,"(3a)") "HDF5> ERROR HDF5 is not available for ",lnSplit(2)%chr(1:4)," blocks."
      call prror(-1)
    end select
  
  case default
    write(lout,"(3a)") "HDF5> ERROR Unrecognised statement '",lnSplit(1)%chr,"'."
    call prror(-1)
  
  end select
  
  ! Report if debugging is ON
  if(h5_debugOn) then
    write (lout,"(a,i3,3a)")  "HDF5> DEBUG Input line len=",len(inLine%chr),": '",chr_trim(inLine%chr),"'."
    write (lout,"(a,i2,3a)") ("HDF5> DEBUG  * Field(",i,") = '",lnSplit(i)%chr,"'",i=1,nSplit)
  end if
  
end subroutine h5_parseInputLine

! ================================================================================================ !
end module hdf5_output

+ei
