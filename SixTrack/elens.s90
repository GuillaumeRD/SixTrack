+dk elens

module elens
  use parpro
  use floatPrecision
  implicit none
  
  ! M. Fitterer (FNAL), A. Mereghtti (CERN, BE-ABP-HSS)
  ! last modified: 28-02-2018
  ! Common block for electron lens definition

  integer, save          :: ielens(nele)              ! index of elens:
  integer, parameter     :: nelens=100                ! size of table with elens data
  integer, save          :: melens                    ! last elens read
  integer, parameter     :: nelens_radials=5          ! max number of radial profiles in memory (in SINGLE ELEMENT array)
  integer, save          :: melens_radials            ! last radial profile read
  
  ! variables to save elens parameters for tracking etc.
  integer, save          :: elens_type(nelens)        ! integer for elens type
                                                      ! 0 : Un-initialized.
                                                      ! 1 : (Hollow annular) elens, uniform electron profile
                                                      ! 2 : (Hollow annular) elens, Gaussian electron profile
                                                      ! 3 : (Hollow annular) elens, radial electron profile from file
  real(kind=fPrec), save :: elens_theta_r2(nelens)    ! kick strength at R2 [mrad]
  real(kind=fPrec), save :: elens_r2(nelens)          ! outer radius R2 [mm]
  real(kind=fPrec), save :: elens_r1(nelens)          ! inner radius R1 [mm]
  real(kind=fPrec), save :: elens_offset_x(nelens), elens_offset_y(nelens)  ! hor./vert. offset of elens [mm]
  real(kind=fPrec), save :: elens_sig(nelens)         ! sig (Gaussian profile) [mm]
  real(kind=fPrec), save :: elens_geo_norm(nelens)    ! normalisation of f(r)
  real(kind=fPrec), save :: elens_len(nelens)         ! length of eLens (e-beam region) [m]
  real(kind=fPrec), save :: elens_I(nelens)           ! current of e-beam [A]
  real(kind=fPrec), save :: elens_Ek(nelens)          ! kinetic energy of e-beam [keV]
  integer, save          :: elens_iradial(nelens)     ! index of radial profile

  ! radial profiles
  integer, parameter     :: elens_unit=107            ! unit for reading the radial profiles
  integer, parameter     :: elens_max_points=500      ! max number of points in radial profiles
  integer, parameter     :: elens_lFileName=16        ! length of filenames
  character(len=elens_lFileName), save :: elens_filename(nelens_radials)  ! file names
  real(kind=fPrec), save :: elens_radials_r(elens_max_points,nelens_radials), & ! profile: r [mm]
 &                          elens_radials_i(elens_max_points,nelens_radials), & ! profile: f []
 &                          elens_radials_iTot(nelens_radials)                  ! total current in profile [A]
  integer, save          :: elens_radials_m(nelens_radials)   ! number of points in profile


contains


  subroutine elens_kick(jelens)

    ! M. Fitterer (FNAL), and A. Mereghetti (CERN, BE-ABP-HSS)
    ! last modified: 28-02-2018
    ! apply kick of electron lens
    
    use crcoall
    use mod_common
    use mod_commonmn
    use mathlib_bouncer
    use numerical_constants
    use physical_constants
    use utils
+ca commonm1
        
    real(kind=fPrec) :: xx, yy, rr, frr
    integer          :: j, jelens
        
    do j=1,napx
       
       ! 1) apply offset of e-lens
       xx=xv(1,j)-elens_offset_x(jelens)
       yy=xv(2,j)-elens_offset_y(jelens)
       
       ! 2) calculate radius of particle relative to center of elens beam
       rr=sqrt(xx**2+yy**2)
       
       ! 3) calculate kick
       !    shape function: spatial charge density depends on type:
       !    0        if r < R1
       !    frr if R1 < r < R2
       !    1        if r > R2
       if (rr.gt.elens_r1(jelens)) then ! rr <= r1 -> no kick from elens
          if (rr.lt.elens_r2(jelens)) then ! r1 < rr < r2
             select case (elens_type(jelens))
             ! NB: take into account negative charge!
             case (1)
                ! UNIFORM: eLens with uniform profile
                ! formula: (r^2-r1^2)/(r2^2-r1^2)
                frr=-( (rr+elens_r1(jelens))*(rr-elens_r1(jelens)) )/elens_geo_norm(jelens)
             case (2)
                ! GAUSSIAN: eLens with Gaussian profile
                ! formula: (exp(-r1^2/2sig^2)-exp(-r^2/2sig^2))/(exp(-r1^2/2sig^2)-exp(-r2^2/2sig^2))
                frr=-( exp_mb(-0.5*(elens_r1(jelens)/elens_sig(jelens))**2)    &
                      -exp_mb(-0.5*(rr              /elens_sig(jelens))**2) )/ &
                       elens_geo_norm(jelens)
             case (3)   
                ! RADIAL: eLens with radial profile from txt file
                frr=-lininterp( rr, elens_radials_r(1:elens_radials_m(elens_iradial(jelens)),elens_iradial(jelens)), &
      &                             elens_radials_i(1:elens_radials_m(elens_iradial(jelens)),elens_iradial(jelens)), &
      &                             elens_radials_m(elens_iradial(jelens)))/elens_geo_norm(jelens)
             end select
          else ! r1 < r2 <= rr
             frr=one
          endif
          ! get actual kicks from frr
          frr = elens_r2(jelens)/rr *frr *elens_theta_r2(jelens)
          ! split kick in its two components and take into account magnetic rigidity:
          yv(1,j)=yv(1,j)+frr*xx/rr*oidpsv(j)
          yv(2,j)=yv(2,j)+frr*yy/rr*oidpsv(j)
       endif
    end do
    return
    
  end subroutine elens_kick
      
      
  subroutine elens_comnul
        
    ! M. Fitterer (FNAL), A. Mereghetti (CERN, BE-ABP-HSS)
    ! last modified: 09-02-2018
    ! always in main code
    
    ! elensparam - used for tracking (parameters of single element)
    
    use mod_common
    use mod_commonmn
    use numerical_constants
    
    integer          :: i, j
    
    do i=1,nele
       ielens(i) = 0
    end do
    melens=0
    do i=1,nelens
       elens_type(i)          = 0
       elens_iradial(i)       = 0
       elens_theta_r2(i)      = zero
       elens_r2(i)            = zero
       elens_r1(i)            = zero
       elens_offset_x(i)      = zero
       elens_offset_y(i)      = zero
       elens_sig(i)           = zero
       elens_geo_norm(i)      = zero
       elens_len(i)           = zero
       elens_I(i)             = zero
       elens_Ek(i)            = zero
    end do
    
    ! radial profiles
    do i=1,nelens_radials
       do j=1,elens_lFileName
          elens_filename(i)(j:j)=' '
       end do
       do j=1,elens_max_points
          elens_radials_r(j,i)=zero
          elens_radials_i(j,i)=zero
       end do
       elens_radials_m(i)=0
       elens_radials_iTot(i)=zero
    end do

    return
  end subroutine elens_comnul
  
  
  real(kind=fPrec) function elens_ctheta_r2( len, Int, Ekin, r2 )
    
    ! A.Mereghetti, BE-ABP-HSS
    ! last modified: 2018-02-21
    ! always in main code
    
    ! compute eLens theta at r2
    ! input variables:
    ! - length of eLens [m];
    ! - current intensity of e-beam [A]
    ! - kinetic energy of electrons [keV]
    ! - outer radius [mm]
    
    use mathlib_bouncer
    use physical_constants
    use mod_common
    use numerical_constants
+ca commonm1
        
    real(kind=fPrec) gamma, beta_e, beta_b, brho, len, Int, Ekin, r2
    gamma=Ekin*c1m3/pmae+1 ! from kinetic energy
    beta_e=sqrt((gamma+one)*(gamma-one))/(gamma)
    beta_b=e0f*gammar/pma
    brho=e0f/(clight*c1m6)
    ! r2: from mm to m
    ! theta: from rad to mrad
    elens_ctheta_r2=len*abs(Int)/(2*pi*eps0*brho*clight**2*r2*c1m3)*c1e3
    if ( Int.lt.zero ) then
       elens_ctheta_r2=elens_ctheta_r2*(one/(beta_e*beta_b)+one)
    else
       elens_ctheta_r2=elens_ctheta_r2*(one/(beta_e*beta_b)-one)
    end if
  end function elens_ctheta_r2
  
      
  subroutine elensParseRadialProfile(ifile)
    
    ! A.Mereghetti (CERN, BE-ABP-HSS)
    ! last modified: 2018-03-01
    ! always in main code
        
    ! read file with radial profile
    ! ifile is index of file in table of radial profiles
    
    ! file is structured as:
    !    r [mm] j [A/cm2]
    ! the current density is then integrated radially;
    ! a couple of points expresses the current density
    !    at the upper edge of the radial bin;
    ! the first line should look like: r_min 0.0,
    !    with r_min lower than the first radius with a non-zero value;
    ! comment line is headed by '#';
    
    use mathlib_bouncer
    use physical_constants
    use crcoall
    use mod_common
    use numerical_constants
        
+ca comgetfields
    character(len=160) string
    integer ierr, ii, ifile, errno
    real(kind=fPrec) tmpflt, round_near, rlast, rmean, dr
    
    ierr=0
    rlast=zero
    write(lout,*)' Parsing file with radial profile '//elens_filename(ifile)
    open(elens_unit,file=elens_filename(ifile),status='old')
    do while (.true.)
       read(elens_unit,'(A)',end=1982,err=1983) string
       if (string(1:1).ne.'#') then
          call getfields_split( string, getfields_fields, getfields_lfields, &
               getfields_nfields, getfields_lerr )
          if ( getfields_lerr ) then
             ierr=1
             goto 1983
          end if
          if (getfields_nfields.lt.2) then
             ierr=2
             goto 1983
          end if
          
          ! actually parse the line
          
          ! - acquire r
+if .not.crlibm
          read (getfields_fields(1)(1:getfields_lfields(1)),*) tmpflt
+ei
+if crlibm
          tmpflt=round_near(errno,getfields_lfields(1)+1,getfields_fields(1))
          if (errno.ne.0) call rounderr (errno,getfields_fields,1,tmpflt)
+ei
          !   check that radial coordinate in profile is increasing
          if ( tmpflt.lt.rlast ) then
             ierr=3
             goto 1983
          end if
          elens_radials_m(ifile)=elens_radials_m(ifile)+1
          if(elens_radials_m(ifile).gt.elens_max_points) then
             ierr=4
             goto 1983
          end if
          elens_radials_r(elens_radials_m(ifile),ifile)=tmpflt
          rlast=elens_radials_r(elens_radials_m(ifile),ifile)
          ! - acquire j
+if .not.crlibm
          read (getfields_fields(2)(1:getfields_lfields(2)),*) tmpflt
+ei
+if crlibm
          tmpflt=round_near(errno,getfields_lfields(2)+1,getfields_fields(2))
          if (errno.ne.0) call rounderr (errno,getfields_fields,2,tmpflt)
+ei
          elens_radials_i(elens_radials_m(ifile),ifile)=tmpflt
          ! check first value is 0.0:
          if(elens_radials_m(ifile).eq.1) then
             if(elens_radials_i(elens_radials_m(ifile),ifile).ne.zero)then
                ierr=5
                goto 1983
             endif
          endif
       end if ! close if for non-comment chars
    end do
1982 close(elens_unit)
    
    ! echo parsed data
    write(lout,fmt='(/,A,1X,I4)') ' Radial profile as from file ' &
          //elens_filename(ifile)//' - #',ifile
    do ii=1,elens_radials_m(ifile)
       write(lout,fmt='(1(1X,I4)," : ",2(1PE10.3,1X))') ii, elens_radials_r(ii,ifile), &
   &         elens_radials_i(ii,ifile)
    end do

    ! integrate function radially
    write(lout,*)' integrating profile radially...'
    do ii=2,elens_radials_m(ifile)
       rmean=(elens_radials_r(ii,ifile)+elens_radials_r(ii-1,ifile))/two*c1m1 ! from mm to cm
       dr=(elens_radials_r(ii,ifile)-elens_radials_r(ii-1,ifile))*c1m1        ! from mm to cm
       elens_radials_i(ii,ifile)=elens_radials_i(ii-1,ifile)+ &
   &         elens_radials_i(ii,ifile)*two*pi*rmean*dr
    end do

    ! normalise
    write(lout,*)' normalising profile...'
    elens_radials_iTot(ifile)=elens_radials_i(elens_radials_m(ifile),ifile)
    do ii=1,elens_radials_m(ifile)
       elens_radials_i(ii,ifile)=elens_radials_i(ii,ifile)/elens_radials_iTot(ifile)
    end do

    ! echo normalised data
    write(lout,*) ' Integrated radial profile as after normalisation:'
    write(lout,fmt='(/,A,1X,1PE10.3,1X,A)') ' - Integrated total current:',elens_radials_iTot(ifile),'A'
    do ii=1,elens_radials_m(ifile)
       write(lout,fmt='(1(1X,I4)," : ",2(1PE10.3,1X))') ii, elens_radials_r(ii,ifile), &
   &         elens_radials_i(ii,ifile)
    end do

    return
    
1983 write(lout,*) 'ERROR ',ierr,' while acquiring radial profile from file '//elens_filename(ifile)
    call prror(-1)
    
  end subroutine elensParseRadialProfile
   

end module elens
 
